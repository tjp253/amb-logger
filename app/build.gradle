apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId "uk.ac.nottingham.eaxtp1.CradleRideLogger"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 73
        versionName "3.5.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        resValue "string", "app_name", "CradleRide Logger"
        buildConfigField 'boolean', 'CROWD_MODE', 'false'
        buildConfigField 'boolean', 'AMB_MODE', 'false'
        buildConfigField 'boolean', 'TEST_MODE', 'false'
        resValue 'integer', 'timeout_default', '1'
        resValue 'integer', 'timeout_min', '1'
        resValue 'integer', 'timeout_max', '5'
        resValue 'bool', 'delete_files_by_default', 'true'
        resValue 'string', 'suffix', '-END'
        resValue 'bool', 'launcher_in_toolbar', 'false'
        resValue 'bool', 'send_to_pi', 'false'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions("1")
    productFlavors {
        core {  // Had to change from 'main' as otherwise it would not sync the gradle.
            buildConfigField 'boolean', 'CROWD_MODE', 'true'
        }
        ambulance {
            applicationId "uk.ac.nottingham.eaxtp1.ambulancelogger"
            buildConfigField 'boolean', 'AMB_MODE', 'true'
            resValue "string", "app_name", "Ambulance Logger"
            resValue 'integer', 'timeout_default', '10'
            resValue 'integer', 'timeout_min', '5'
            resValue 'integer', 'timeout_max', '15'
            resValue 'string', 'suffix', '-AMB'
            resValue 'bool', 'launcher_in_toolbar', 'true'
        }
        shaker {
            applicationIdSuffix ".shaker"
            versionNameSuffix ' - Test Mode'
            buildConfigField 'boolean', 'TEST_MODE', 'true'
            resValue "string", "app_name", "CradleRide Shaker"
            resValue 'bool', 'delete_files_by_default', 'false'
            resValue 'bool', 'send_to_pi', 'true'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.google.code.gson:gson:2.8.8' // read in hospital coords from JSON
    implementation 'com.squareup.okhttp3:okhttp:4.2.2' // handle easy uploads
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'com.vanniktech:vntnumberpickerpreference:1.0.0'
}
